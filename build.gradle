import org.gradle.internal.jvm.Jvm
import org.gradle.util.GradleVersion

apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

version = '1.0'
def mainClass = System.getProperty('mainClass')
mainClassName = mainClass != null ? mainClass : "vim.tools.App"

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all' << '-Xdoclint:syntax' << '-Xdiags:verbose'
}

test {
    testLogging.quiet.events = ['failed', 'standard_out', 'standard_error']
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'com.github.spullara.mustache.java:compiler:0.8.16'
    compile 'com.google.code.gson:gson:2.3'
    compile 'commons-cli:commons-cli:1.2'
    compile 'org.eclipse.tycho:org.eclipse.jdt.core:3.10.0.v20140604-1726'
    compile ('org.eclipse.text:org.eclipse.text:3.5.101') {
        exclude group: 'org.eclipse.core', module: 'org.eclipse.core.commands'
    }
    compile ('org.eclipse.equinox:org.eclipse.equinox.common:3.6.0.v20100503') {
        exclude group: 'org.eclipse.osgi', module: 'org.eclipse.osgi'
    }
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

jar {
    manifest.attributes 'Main-Class': "$mainClassName"
    manifest.attributes 'Entry-Point': "vim.tools.App,vim.tools.EclipseFormatter,vim.tools.MustacheGenerator,vim.tools.StartSession"
    manifest.attributes 'Version': "$version"
    manifest.attributes 'Created-By': Jvm.current()
    manifest.attributes 'Gradle-Version': GradleVersion.current()
    manifest.attributes 'Class-Path': configurations.runtime.files.collect { it.name }.join(' ')
}

